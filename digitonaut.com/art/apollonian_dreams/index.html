<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/apollonian_dreams/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 07:28:12 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Apollonian Dreams</title>

<link type="text/css" rel="stylesheet" href="main.css">
</head>
<body>

<script id="vs" type="not-js">
void main()	{
  gl_Position = vec4( position, 1.0 );
}
</script>
<script id="fs" type="not-js">
precision mediump float;

// -------------------------------------------- code from https://glslsandbox.com/ ------------------

/*
 * Original shader from: https://www.shadertoy.com/view/WdfXR4
 */

#ifdef GL_ES
precision mediump float;
#endif

// glslsandbox uniforms
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

// shadertoy emulation
#define iTime time
#define iResolution resolution

// --------[ Original ShaderToy begins here ]---------- //
// Created by krakel, 2019 - mods by sphinx
// License: Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License

#define sqrt3 sqrt(3.)
#define pixelRadius .5/iResolution.x
#define Time (iTime/32.)

//this AA is expensive and rather ineffective
#define AA 0

vec3 sunDir = normalize(vec3(0, 0, 1));
float viewDist = 30.;
vec3 sunLight = vec3(2.,2.,1.6);
vec3 ambLight = vec3(.5, .8, .9);
vec3 fogColor = vec3(.8,.8,.67);

vec3 triangles(vec3 p){
    float zm = 1.;
    p.x = p.x-sqrt3*(p.y+.5)/3.;
	p = vec3(mod(p.x+sqrt3/2.,sqrt3)-sqrt3/2., mod(p.y+.5,1.5)-.5 , mod(p.z+.5*zm,zm)-.5*zm);
	p = vec3(p.x/sqrt3, (p.y+.5)*2./3. -.5 , p.z);
	p = p.y>-p.x ? vec3(-p.y,-p.x , p.z) : p;
	p = vec3(p.x*sqrt3, (p.y+.5)*3./2.-.5 , p.z);
	return vec3(p.x+sqrt3*(p.y+.5)/3., p.y , p.z);
}
float sdf(vec3 p){
    float scale = 1.;
	float phi = sqrt(5.)*.5-.5;
	float s = sqrt(1./5.);
	for( int i=0; i<10;i++ )
	{
		p = triangles(p);
		float r2= dot(p/phi,p*-phi);
		float k = s/r2;
		p = p * k;
		scale=scale*k;
    }
	vec3 a = abs(p);
	   float y = max(max(a.x, a.y),a.z);
	float z = dot(p, vec3(1., 1., 1.));
	float r = length(p);
	
	return .2*y/scale-.000025;
}

float march(vec3 ro, vec3 rd){
    float t = sdf( ro+rd)-.1;
    float h = length(rd*.45);
    float r = pixelRadius;
	if(t > h) return -t;
    for( float i=1.; i<128.; i++ )
    {
        if(h<t*r || t>viewDist){break;}

	    h = max(h, 0.);
	    r *= 1.;

            t += h+.001;
	    h = sdf( ro+rd*t)-(i*.0000005);
	    t *= 1.; 
    }
    if(h>t*r){t= viewDist;}
    return t;
}

vec3 getNormal( in vec3 p, in float t )
{
    float precis = max(0.000025 * t, 0.) + .00000001;

    vec2 e = vec2(1.0,-1.)*precis;
    return normalize( e.xyy*sdf( p + e.xyy ) + 
					  e.yyx*sdf( p + e.yyx ) + 
					  e.yxy*sdf( p + e.yxy ) + 
                      e.xxx*sdf( p + e.xxx ) );
}
float ao(vec3 p,vec3 n){
	float r=0.;
    float t=0.;
	for( int i=0; i<3;i++ )
	{
		t=t+.05;
		r=r + sdf(p+n*t)/t;
    }
    return smoothstep(-.0,2.,r);
}

float shadowRay(vec3 p,vec3 n,vec3 ld){
    p = p + n*.0012;
    float t = .5;
    float h = 1.;
    for( int i=0; i<16; i++ )
    {
        if(h<t*pixelRadius || t>2.){break;}
	    h = sdf( p+ld*t );
        t += h;
    }
    return smoothstep(0.,1.,t*t+1.)*.25;
}

vec3 fog(float t, vec3 col, float density){
   return  mix(col , fogColor, 1.-exp(-t*density));
}

float pulse(float t){
    return pow(t,12.);
}

vec3 render(vec3 ro, vec3 rd){
    float t= march(ro,rd);
    vec3 col = vec3(0);
    if(t<viewDist){
        vec3 p = ro+t*rd;
    	vec3 n = getNormal(p, t);
//
        col = sunLight*vec3 (dot(n, sunDir ) );
        col *= max(shadowRay(p,n,sunDir), 0.);
        float ar = clamp(ao(p,n), 0., 1.);
	float pls1 = pulse(abs(sin(length(p)+abs(fract(3.*t)-.5)*12.-abs(fract(5.*t)-.5)*7.+time*.5)));
 	float pls2 = pulse(abs(cos(sin(t)*32.-time*.1)));
	    float pls = pls1 * pls2*2.+(pls2)*.125;
        col 	+= ambLight*ar;
        vec3 rq = getNormal(p+n*.001, (t+t/pls));
	col 	+= clamp(abs(dot(rq, n))*length((n+2.)/(rq-3.)), 0., 1.)*.125;
        col 	+= n.x*.215+n.y*.001;
	col.z    *= .5;
	col 	= fog(t, (n.y*-n.z)*.144+abs(col.zzx-col.zzx*-pls)-.25, abs(t-.5)*.1+.15);
	

    }else{
        col = fogColor*1.;
    }
    return col;
}

vec3 jitter( int i){
    return pixelRadius* fract(123.*sin(vec3(15,17,19)*float(i)));
}

vec3 AArender(vec3 ro, vec3 rd){
    if(AA>1){
    vec3 col = vec3(0);
    for( int i=0; i<AA; i++){
        col += render(ro, normalize(rd+jitter(i)));
    }
        return col/float(AA);}
    else{return render(ro,rd);}
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = fragCoord/iResolution.x - .5*iResolution.xy/iResolution.x;
    
    vec3 camPos = vec3(0,1,.5) + Time*vec3(1) + .5*vec3(sin(Time), cos(Time) ,0 );
    vec3 camDir = normalize(vec3(1,0,1) + vec3(0, cos(Time),sin(Time)) );
    vec3 camRi  = normalize(cross(camDir, vec3(0,sin(Time),cos(Time))));
    vec3 camUp  = normalize(cross(camDir, camRi));
    
    sunDir = normalize(vec3(0, sin(Time),cos(Time)));
    sunLight = vec3(sin(Time),cos(Time),0);
    
    fragColor = vec4(AArender(camPos, normalize(camDir + camRi*uv.x + camUp*uv.y) ), 1.0);
}
// --------[ Original ShaderToy ends here ]---------- //

void main(void)
{
    mainImage(gl_FragColor, gl_FragCoord.xy);
}
// -------------------------------------------------------------------------------------------------------------------

</script>
<script src="three.min.js"></script>
<script src="main.js"></script>

</body>

<!-- Mirrored from digitonaut.com/art/apollonian_dreams/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 07:28:17 GMT -->
</html>
