<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/animated_subdivision/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 07:27:49 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Animated Subdivision</title>

<link type="text/css" rel="stylesheet" href="main.css">
</head>
<body>

<script id="vs" type="not-js">
void main()	{
  gl_Position = vec4( position, 1.0 );
}
</script>
<script id="fs" type="not-js">
precision mediump float;

// -------------------------------------------- code from https://glslsandbox.com/ ------------------

/*
 * Original shader from: https://www.shadertoy.com/view/fsyGD3
 */

#ifdef GL_ES
precision mediump float;
#endif

// glslsandbox uniforms
uniform float time;
uniform vec2 resolution;

// shadertoy emulation
#define iTime time
#define iResolution resolution

// --------[ Original ShaderToy begins here ]---------- //
// Fork of "Rectagular Subdivisor" by Tater. https://shadertoy.com/view/7sV3WD
// 2021-09-21 21:08:49

#define pi 3.1415926535
float h21 (vec2 a) {
    return fract(sin(dot(a.xy,vec2(12.9898,78.233)))*43758.5453123);
}
float h11 (float a) {
    return fract(sin((a)*12.9898)*43758.5453123);
}
//iq palette
vec3 pal( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d ){
    return a + b*cos(2.*pi*(c*t+d));
}
float box(vec2 p, vec2 b){
    vec2 d = abs(p)-b;
    return max(d.x,d.y);
}
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 R = iResolution.xy;
    vec2 uv = (fragCoord-0.5*R.xy)/R.y;
    vec3 col = vec3(0);
    float t = mod(iTime*1.2,6000.);
    float px = 1./iResolution.y;
    
    vec2 dMin = vec2(-0.5);
    vec2 dMax = vec2(0.5);
    dMin.x*=R.x/R.y;
    dMax.x*=R.x/R.y;
    vec2 dim = dMax - dMin;
    float id = 0.;
    const float ITERS = 14.;
    float seed = floor(t/ITERS)+0.2;

    float a;
    t = mod(t,ITERS);
    
    float MIN_SIZE = 0.015;
    //float ITERS = its;
    float BORDER_SIZE = 0.003;
    float MIN_ITERS = 1.;
    float borderSize = 0.003;

    //BIG THANKS to @0b5vr for letting me use his cleaner subdiv formula
    //https://www.shadertoy.com/view/NsKGDy
    
    for(float i = 0.;i<ITERS;i++){
        // divide the box into quads
        vec2 divHash = vec2(
            h21( vec2( i + id, seed )),
            h21( vec2( i + id + 2.44, seed ))
        );
        vec2 divide = divHash * dim + dMin;
        
        //Clamp division line
        //Interestingly this doesn't work as expected but the result is more interesting
        //If you include a 1.01 factor on the MINE_SIZE it will divide nearly all the space
        divide = clamp(divide, dMin + MIN_SIZE, dMax - MIN_SIZE);
        
        //Un-altered division line for coloring moving cells 
        vec2 divideFull = divide;
        
        //Find the minimum dimension size
        vec2 minAxis = min(abs(dMin - divide), abs(dMax - divide));
        float minSize = min( minAxis.x, minAxis.y);
        
        //if minimum dimension is too small break out
        bool smallEnough = minSize < MIN_SIZE;
        if (smallEnough && i + 1. > MIN_ITERS) { break; }
        
        //If the current iteration is the leading one smooth the division line
        float tt = smoothstep(0.,1.,fract(t));
        if(i == floor(t) &&mod(t,2.0)<1.0){
            divide=mix(dMin,divide,tt);
        }
        else if(i == floor(t)){
            divide=mix(dMax,divide,tt);
        }
        if(i>floor(t)) break;

        // update the box domain
        dMax = mix( dMax, divide, step( uv, divide ));
        dMin = mix( divide, dMin, step( uv, divide ));

        // id will be used for coloring and hash seeding
        //vec2 diff = mix( -divide, divide, step( uv, divide));
        vec2 diff2 = mix( -divideFull, divideFull, step( uv, divide));
        id = length(diff2+10.0)*100.0;
        
        // recalculate the dimension
        dim = dMax - dMin;

    }
    //Shrink boxes if before changing seed
    float shr =1.0-abs(pow(abs(cos(t*pi/ITERS)),100.0));
    //Calculate 2d box sdf
    vec2 center = (dMin + dMax)/2.0;
    a = box(uv-center,dim*0.5*shr);
    
    //Color box
    id = h11(id)*1000.0;
    vec3 e = vec3(0.5);
    vec3 al = pal(id*0.1,e*1.2,e,e*2.0,vec3(0,0.33,0.66));
    col = clamp(al,0.,1.);
    col-=smoothstep(-px,px,a+borderSize);
    //col = vec3(-a*10.0);
    fragColor = vec4(col,1.0);
}
// --------[ Original ShaderToy ends here ]---------- //

void main(void)
{
    mainImage(gl_FragColor, gl_FragCoord.xy);
}
// -------------------------------------------------------------------------------------------------------------------

</script>
<script src="three.min.js"></script>
<script src="main.js"></script>

</body>

<!-- Mirrored from digitonaut.com/art/animated_subdivision/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 07:27:51 GMT -->
</html>
